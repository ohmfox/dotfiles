#
# A simple theme from Cendaeg.
#
# Authors:
#   Conlin Durbin <conlindurbin@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
#

function prompt_cendaeg_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi
}

function prompt_cendaeg_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add a hook for calling info functions before each command.
  add-zsh-hook precmd prompt_cendaeg_precmd

  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'no'
  zstyle ':prezto:module:git:info:action' format ' +%s'
  zstyle ':prezto:module:git:info:branch' format ' %F{8}%b%f'
  zstyle ':prezto:module:git:info:commit' format ' %F{white}%.7c%f'
  zstyle ':prezto:module:git:info:indexed' format ' '
  zstyle ':prezto:module:git:info:unindexed' format ' '
  zstyle ':prezto:module:git:info:untracked' format ' '
  zstyle ':prezto:module:git:info:dirty' format ' %F{8}✗%f'
  zstyle ':prezto:module:git:info:keys' format 'rprompt' '%b%c%s%D'

  # Define prompts.
  PROMPT="
%~
%(?.%F{brightwhite}${1:-☻ }%f.%F{white}${1:-☻ }%f) "
RPROMPT='${git_info[rprompt]}'

}

function prompt_cendaeg_help {
  cat <<EOH
This prompt's last command exit status symbol is customizable:

 jprompt cendaeg [<symbol>]

If this option is not provided, the symbol defaults to ☻.
EOH
}

function prompt_cendaeg_preview {
  local +h PROMPT='%# '
  local +h RPROMPT=''
  local +h SPROMPT=''

  if (( $# > 0 )); then
    prompt_preview_theme 'cendaeg' "$@"
  else
    prompt_preview_theme 'cendaeg'
    print
    prompt_preview_theme 'cendaeg' "❮"
    print
    prompt_preview_theme 'cendaeg' "$"
  fi
}

prompt_cendaeg_setup "$@"
